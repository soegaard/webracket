(define width  256)
(define height 256)

(define canvas (js-create-element "canvas"))
(js-set-canvas-width!  canvas width)
(js-set-canvas-height! canvas height)
(js-append-child! (js-document-body) canvas)

(define ctx (js-canvas-get-context canvas "2d" (js-undefined)))

(define (draw t)
  (define ft (* 0.02 (exact->inexact t)))
  (define img (js-canvas2d-create-image-data ctx
                                             (exact->inexact width)
                                             (exact->inexact height)))
  (js-set-property! (js-global-this) "imgData" img)
  (define data (js-eval "imgData.data"))
  (let loop-y ([y 0])
    (when (< y height)
      (let loop-x ([x 0])
        (when (< x width)
          (define fx (exact->inexact x))
          (define fy (exact->inexact y))
          (define intensity (+ 2.
                               (flsin (/ (+ fx ft) 16.))
                               (flsin (/ (+ fy (* 2. ft)) 8.))))
          (define c (inexact->exact (floor (* 64. intensity))))
          (define r (remainder    c      256))
          (define g (remainder (+ c  85) 256))
          (define b (remainder (+ c 170) 256))
          (define idx (* 4 (+ (* y width) x)))
          (js-set-property! data idx r)
          (js-set-property! data (add1 idx) g)
          (js-set-property! data (+ idx 2) b)
          (js-set-property! data (+ idx 3) 255)
          (loop-x (add1 x))))
      (loop-y (add1 y))))
  (js-canvas2d-put-image-data ctx img 0. 0.
                              (js-undefined) (js-undefined)
                              (js-undefined) (js-undefined))
  (js-window-request-animation-frame (procedure->external draw)))

(js-window-request-animation-frame (procedure->external draw))
